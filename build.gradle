plugins {
    id 'java'
    id 'application'
    id 'org.javamodularity.moduleplugin' version '1.8.12'
    id 'org.openjfx.javafxplugin' version '0.0.13'
    id 'org.beryx.jlink' version '3.1.1'
}

group 'com.financialtracker'
version '1.0'

repositories {
    mavenCentral()
}

ext {
    junitVersion = '5.10.2'
}

sourceCompatibility = '22'
targetCompatibility = '22'

tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
}

application {
    mainModule = 'com.financialtracker'
    mainClass = 'com.financialtracker.HelloApplication'
}

javafx {
    version = '22.0.1'
    modules = ['javafx.controls', 'javafx.fxml']
}

dependencies {
    implementation("org.xerial:sqlite-jdbc:3.44.1.0")
    testImplementation("org.junit.jupiter:junit-jupiter-api:${junitVersion}")
    testRuntimeOnly("org.junit.jupiter:junit-jupiter-engine:${junitVersion}")
}

test {
    useJUnitPlatform()
}

jlink {
    launcher {
        name = "FinancialTracker"
    }
    jpackage {
        imageName = "FinancialTracker"
        def osName = System.getProperty("os.name").toLowerCase()
        if (osName.contains("mac")) {
            installerType = "dmg"
            installerOptions = [
                "--icon", file("src/main/resources/com/financialtracker/favicon.icns").absolutePath
            ]
            imageOptions = [
                "--icon", file("src/main/resources/com/financialtracker/favicon.icns").absolutePath
            ]
        } else if (osName.contains("linux")) {
            installerType = "deb"
            installerOptions = [
                "--icon", file("src/main/resources/com/financialtracker/favicon.png").absolutePath
            ]
            imageOptions = [
                "--icon", file("src/main/resources/com/financialtracker/favicon.png").absolutePath
            ]
        } else if (osName.contains("windows")) {
            installerType = "exe"
            installerOptions = [
                "--icon", file("src/main/resources/com/financialtracker/favicon.ico").absolutePath
            ]
            imageOptions = [
                "--icon", file("src/main/resources/com/financialtracker/favicon.ico").absolutePath
            ]
        } else {
            throw new GradleException("Unsupported operating system: " + osName)
        }
    }
}
jlinkZip {
    group = 'distribution'
}